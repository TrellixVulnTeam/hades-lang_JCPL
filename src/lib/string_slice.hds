
import libc as c;
import lib.panic as Panic;
import hades.memory as Memory;
import hades.drop as Drop;
import lib.string as String;
import lib.assert as Assert;

const panic = Panic.panic;
const assert = Assert.assert;

struct t {
    val m_length: Size;
    val m_buffer: *Byte;
}

def from_static(buffer: *Byte): t {
    return from_c(buffer);
}

def from_c(buffer: *Byte): t {
    val length = c.strlen(buffer);
    return t(length, buffer);
}


def length(this: t): Size {
    return this.m_length;
}

def _byte_at(this: t, offset: Size): Byte {
    if offset >= this.length() {
        panic(b"String::_byte_at overflow");
    }
    val offset_ptr = this._buffer().offset(offset);
    return *offset_ptr;
}

def _buffer(this: t): *Byte {
    return this.m_buffer;
}

def equals(this: t, that: t): Bool {
    val lengths_not_match = this.length() != that.length();
    if lengths_not_match {
        return false;
    }
    if this._buffer() == that._buffer() {
        return true;
    }
    val mut offset: Size = 0;
    while offset < this.length() {
        if this._byte_at(offset) != that._byte_at(offset) {
            return false;
        }
        offset = offset + 1;
    }
    return true;
}

def starts_with(this: t, other: t): Bool {
    if other.length() > this.length() {
        return false;
    }
    val mut index: Size = 0;
    while index < other.length() {
        if this._byte_at(index) != other._byte_at(index) {
            return false;
        }
        index = index + 1;
    }
    return true;
}

def starts_with_ignore_case(this: t, other: t): Bool {
    // FIXME: Actually ignore case here
    return this.starts_with(other);
}

def as_c_str(this: t): *Byte {
    return this.m_buffer;
}

def parse_size(this: t, base: CInt): Size {
    return c.strtoul(this.as_c_str(), nullptr, base);
}

def drop_first(this: t, count: Size): t {
    assert(this.length() >= count);
    assert(count >= 0);
    return t(this.length() - count, this.as_c_str().offset(count));
}

