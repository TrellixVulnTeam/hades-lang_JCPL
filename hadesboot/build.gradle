plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
}

def logbackVersion = "1.2.3"
def slf4jVersion = "1.7.30"
group 'org.hades'
version '0.1-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "ch.qos.logback:logback-core:$logbackVersion"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVersion"
    implementation group: "org.apache.commons", name: 'commons-lang3', version: '3.10'
    implementation group: 'com.diogonunes', name: 'JColor', version: '5.0.0'

    implementation "org.bytedeco:llvm-platform:10.0.0-1.5.3"

    testImplementation "org.jetbrains.kotlin:kotlin-test:1.3.70"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.2.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
}

test {
    workingDir = new File("..")
    useJUnitPlatform()
    afterTest { descriptor, result ->
        logger.quiet "[TEST] [${result.resultType}] ${descriptor.className}::${descriptor.displayName}"
    }

    afterSuite { descriptor, result ->
        if (!descriptor.parent) {
            println()
            logger.quiet "[TESTS] [${result.resultType}] (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions.useIR = true
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

application {
    mainClassName = 'hadesc.MainKt'
}
jar {
    manifest {
        attributes "Main-Class": "hadesc.MainKt"
    }
}

task copyStdlib(type: Copy) {
    from fileTree(dir: '../stdlib')
    into 'build/install/hadesboot/stdlib'
}

installDist.finalizedBy(copyStdlib) {
}
